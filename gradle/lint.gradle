def domicLintOptions() {
    return {
        warningsAsErrors true
        abortOnError true

        textReport true
        textOutput 'stdout'

        htmlReport true
        xmlReport true

        enable 'Interoperability'
    }
}

subprojects { subProject ->
    subProject.afterEvaluate {
        def plugins = subProject.getPlugins()

        def isAndroid = plugins.hasPlugin('com.android.application') || plugins.hasPlugin('com.android.library')
        def isJvm = plugins.hasPlugin('java-library') || plugins.hasPlugin('kotlin')

        if (isAndroid) {
            configure(android.lintOptions, domicLintOptions())
        } else if (isJvm) {
            subProject.plugins.apply('com.android.lint') // Lint supports JVM modules too.
            configure(lintOptions, domicLintOptions())
        }
    }
}
